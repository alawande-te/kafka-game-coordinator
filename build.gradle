buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:4.1.5"
    }
}

plugins {
    id 'org.unbroken-dome.test-sets' version '2.1.1'
    id 'com.avast.gradle.docker-compose' version '0.5.0'
}

group 'anlawande'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task buildDocker {
    doLast {
        println "All images pushed!"
    }
}

configure(subprojects) {
    apply plugin: 'io.freefair.lombok'
}

def nonSpringProjects = ['data-models', 'common'] as HashSet

subprojects {
    if (nonSpringProjects.contains(it.name)) {
        return
    }

    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'docker-compose'

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '11.0.2'
    }

    afterEvaluate { project ->
        def projectAbsPath = projectDir.toPath().toString().replaceAll('\\\\', '/')

        task copyJar(type: Copy) {
            dependsOn project.tasks.bootJar
            from(project.tasks.bootJar.outputs.files.singleFile)
            into("build/dist")
        }
        task copyDocker(type: Copy) {
            from("src/docker")
            into("build/dist")
        }
        task createDocker(type: Exec) {
            dependsOn copyJar
            dependsOn copyDocker
            workingDir "build/dist"
            commandLine "docker", "build", ".", "-t", "$project.name", "--build-arg", "JAR_FILE=$project.name-0.1.0.jar"
            standardOutput = new ByteArrayOutputStream()
            doLast {
                if(execResult.exitValue == 0) {
                    println "Pushed docker image for $project.name"
                } else {
                    println "Failed to create docker image for $project.name"
                }
            }
        }
        buildDocker.dependsOn createDocker

        testSets {
            integrationTest 
        }
        
        def intDockerComposeFilePath = "$projectAbsPath/src/integrationTest/docker/docker-compose.yml"
        if (file(intDockerComposeFilePath).exists()) {
            dockerCompose.isRequiredBy(integrationTest)
            dockerCompose {
                useComposeFiles = [intDockerComposeFilePath]
            }
        }
    }
}
