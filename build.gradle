plugins {
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group 'anlawande'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task buildDocker {
    doLast {
        println "All images pushed!"
    }
}

def nonSpringProjects = ['data-models'] as HashSet

subprojects {
    if (nonSpringProjects.contains(it.name)) {
        return
    }

    apply plugin: 'org.unbroken-dome.test-sets'

    afterEvaluate { project ->
        task copyJar(type: Copy) {
            dependsOn project.tasks.bootJar
            from(project.tasks.bootJar.outputs.files.singleFile)
            into("build/dist")
        }
        task copyDocker(type: Copy) {
            from("src/docker")
            into("build/dist")
        }
        task createDocker(type: Exec) {
            dependsOn copyJar
            dependsOn copyDocker
            workingDir "build/dist"
            commandLine "docker", "build", ".", "-t", "$project.name", "--build-arg", "JAR_FILE=$project.name-0.1.0.jar"
            standardOutput = new ByteArrayOutputStream()
            doLast {
                if(execResult.exitValue == 0) {
                    println "Pushed docker image for $project.name"
                } else {
                    println "Failed to create docker image for $project.name"
                }
            }
        }
        buildDocker.dependsOn createDocker

        testSets {
            integrationTest 
        }
    }
}
